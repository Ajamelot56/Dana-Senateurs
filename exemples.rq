#Sélection universelle
prefix custom: <https://github.com/Ajamelot56/Dana-Senateurs/>

SELECT ?subject ?predicate ?object
FROM custom:general
WHERE {
  ?subject ?predicate ?object
}
LIMIT 25

#Sélection avec prédicat : sénateurs en fonction
PREFIX custom: <https://github.com/Ajamelot56/Dana-Senateurs/>
PREFIX schema: <http://schema.org/>

SELECT ?subject
FROM custom:general
WHERE {
  ?subject custom:actif true
}
# (Il y a bien 348 résultats.)

#Sélection groupée : nombre total de sénateurs ayant appartenu à chaque groupe politique
PREFIX custom: <https://github.com/Ajamelot56/Dana-Senateurs/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?GP (count(distinct(?s)) as ?compteur)
FROM custom:histoGroupes
WHERE {
  ?s schema:affiliation [rdf:value ?GP]
}
GROUP BY ?GP
ORDER BY DESC(?compteur)

#Donc pour avoir les groupes politiques, de seulement les sénateurs actifs... ça va pas être si simple que ça
#histoGroupe.ttl liste les appartenances courantes, ET passées des sénateurs, il faut prendre des triples tel que X rdf:endDate Y n'existe pas.

# Groupes politiques par nombre
PREFIX custom: <https://github.com/Ajamelot56/Dana-Senateurs/>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?GP (count(distinct(?s)) as ?nbSénateurs)
FROM custom:histoGroupes
where {
  ?s schema:affiliation ?x .
  ?x rdf:value ?GP
  filter not exists {
    ?x schema:endDate ?ed
  }
}
group by ?GP
order by desc(?nbSénateurs)




# Requête qui génère les sénateurs actifs qui ont été députés.

PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX schema: <http://schema.org/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX custom: <https://fuseki.dolr.es/ontology/>
PREFIX custom2: <https://github.com/Ajamelot56/Dana-Senateurs/>
PREFIX dbo: <https://dbpedia.org/ontology/>
PREFIX dbo2: <http://dbpedia.org/ontology/>

SELECT Distinct ?depute ?senateur ?Nom ?prenom ?age ?gender ?org ?circ ?cat ?occup

FROM <http://localhost/general>
FROM <http://localhost/parliamentmember>
FROM <http://localhost/circonscriptions>
FROM <http://localhost/organizations>

WHERE {
 ?depute rdf:type custom:ParliamentMember .
 ?senateur custom2:PCS ?cat.
 ?senateur schema:familyName ?Nom.
 ?depute schema:familyName ?Nom.
 ?depute schema:gender ?gender.
 ?senateur schema:givenName ?prenom.
 ?depute schema:givenName ?prenom.
 ?depute dbo:age ?age.
 ?senateur custom2:actif true.
 ?senateur custom2:circonscription / dbo2:officialName ?circ.
 ?depute schema:Organization / schema:alternateName ?org.
 ?senateur schema:hasOccupation / schema:name ?occup

}

LIMIT 100