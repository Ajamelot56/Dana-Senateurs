prefix schema: <http://schema.org/>
prefix wikidata: <http://www.wikidata.org/entity/>
prefix custom: <http://localhost/>

CONSTRUCT {
  ?u schema:honorificPrefix ?Qualité ;
    schema:familyName ?Nom_usuel ;
    schema:givenName ?Prénom_usuel ;
    custom:actif ?actif ;
    schema:birthDate ?dateN ;
    schema:deathDate ?dateD ;
    custom:commissionPermanente ?Commission_permanente ;
    custom:circonscription ?Circonscription ;
    custom:fonctionBureau ?Fonction_au_bureau_du_Sénat ;
    schema:email ?courriel ;
    custom:PCS ?PCS_INSEE ; # Catégorie socio-professionnelle
    schema:hasOccupation [
      schema:occupationalCategory ?Catégorie_professionnelle ;
      schema:name ?Description_de_la_profession
    ] .
}
FROM <file:ODSEN_GENERAL.csv#encoding=cp1252>
WHERE {
  bind (uri(concat('http://localhost/', ?Matricule)) as ?u)
  bind (?État = 'ACTIF' as ?actif)
  bind (strbefore(?Date_naissance, ' ') as ?dateN)
  bind (strbefore(?Date_de_décès, ' ') as ?dateD)
  optional { filter (?Courrier_électronique != 'Non public')
    bind(?Courrier_électronique as ?courriel)
  }
}