prefix schema: <http://schema.org/>
prefix wikidata: <http://www.wikidata.org/entity/>
prefix custom: <https://github.com/Ajamelot56/Dana-Senateurs/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  ?u rdf:type custom:Sénateur ;
    schema:honorificPrefix ?Qualité ;
    schema:familyName ?Nom_usuel ;
    schema:givenName ?Prénom_usuel ;
    custom:actif ?actif ;
    schema:birthDate ?dateN ;
    schema:deathDate ?dateD ;
    custom:commissionPermanente ?Commission_permanente ;
    custom:circonscription ?circ ;
    custom:fonctionBureau ?Fonction_au_Bureau_du_Sénat ;
    schema:email ?courriel ;
    custom:PCS ?PCS_INSEE ; # Catégorie socio-professionnelle
    schema:hasOccupation [
      schema:occupationalCategory ?Catégorie_professionnelle ;
      schema:name ?Description_de_la_profession
    ] .
}
from <file:../csv/ODSEN_GENERAL.csv#encoding=cp1252>
WHERE {
  bind (uri(concat('https://github.com/Ajamelot56/Dana-Senateurs/senateur/', ?Matricule)) as ?u)
  bind (?État = 'ACTIF' as ?actif)
  bind (xsd:date(strbefore(?Date_naissance, ' ')) as ?dateN)
  bind (xsd:date(strbefore(?Date_de_décès, ' ')) as ?dateD)
  optional { filter (?Courrier_électronique != 'Non public')
    bind(?Courrier_électronique as ?courriel)
  }
  bind(uri(concat("https://github.com/Ajamelot56/Dana-Senateurs/", ?Code_Circonscription)) as ?circ)
}